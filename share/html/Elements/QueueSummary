<%INIT>
$queues ||= RT::Queues->new( $session{'CurrentUser'} );
$queues->UnLimit unless $queues->_isLimited;

$m->callback( CallbackName => 'SQLFilter', Queues => $queues );

my @queues = $queue_filter
    ? grep $queue_filter->($_), @{ $queues->ItemsArrayRef }
    : @{ $queues->ItemsArrayRef }
;
$m->callback( CallbackName => 'Filter', Queues => \@queues );

$preference = $preference
    ? $session{'CurrentUser'}->UserObj->Preferences($preference, {})
    : {}
;

my $comp = '/Elements/QueueSummaryByStatus';
if ( $preference->{'SplitByLifecycle'} ) {
    $comp = '/Elements/QueueSummaryByLifecycle';
}
if ( $preference->{'Unwanted'} && keys %{ $preference->{'Unwanted'} } ) {
    @queues = grep !$preference->{'Unwanted'}{ $_->Name }, @queues;
}

return $m->comp( $comp ) unless @queues;

my %lifecycle = map { lc $_->Name => $_ } map $_->Lifecycle, @queues;

unless (@statuses) {
    my %seen;
    foreach my $set ( 'initial', 'active' ) {
        foreach my $lifecycle ( map $lifecycle{$_}, sort keys %lifecycle ) {
            push @statuses, grep !$seen{ lc $_ }++, $lifecycle->Valid($set);
        }
    }
}

use RT::Report::Tickets;
my $report = RT::Report::Tickets->new( RT->SystemUser );
my $query =
    '('. join(" OR ", map "Status = $_", map RT::SQL->Quote($_), @statuses) .')'
    .' AND ('. join(' OR ', map "Queue = ". $_->id, @queues) .')';
$report->SetupGroupings( Query => $query, GroupBy => [qw(Status Queue)] );

my (%data, %data_has_status);
while ( my $entry = $report->Next ) {
    my ($queue, $status, $count) = map $entry->__Value($_), qw(Queue Status id);

    $data{ $queue }{ $status } = $count;
    $data{ $queue }{''} += $count;
    $data_has_status{ $status } = 1;
}

unless ( $preference->{'ShowEmptyColumns'} ) {
    @statuses = grep $data_has_status{$_}, @statuses;
}
unless ( $preference->{'ShowEmptyRows'} ) {
    @queues = grep $data{ $_->id }{''}, @queues;
}

return $m->comp(
    $comp,
    queues   => \@queues,
    statuses => \@statuses,
    data     => \%data,
);
</%INIT>
<%ARGS>
$queues => undef
$queue_filter => undef
$preference => undef,
@statuses => ()
</%ARGS>
