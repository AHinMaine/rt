%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%# 
%# This software is Copyright (c) 1996-2008 Best Practical Solutions, LLC
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
<&| /Widgets/TitleBox, title => _($title)&>

%# Hide all the save functionality if the user shouldn't see it.
% if ( $can_modify ) {
<&|/l&>Privacy:</&>
<& SelectSearchObject, name => 'SavedSearchOwner', objects => \@objects, object => ( $object && $object->id ) ? $object->object : '' &>
<br />
<&|/l&>description</&>:
<input size="25" name="saved_search_description" value="<% $description || '' %>" />

% if ($id ne 'new') {
<nobr>
% if ( $Dirty ) {
<input type="submit" class="button" name="SavedSearchRevert" value="<%_('Revert')%>" />
% }
<input type="submit" class="button" name="SavedSearchDelete" value="<%_('Delete')%>" />
% if ( $allow_copy ) {
<input type="submit" class="button" name="SavedSearchCopy"   value="<%_('Save as New')%>" />
% }
</nobr>
% }
% if ( $object && $object->id ) {
<input type="submit" class="button" name="SavedSearchSave"   value="<%_('Update')%>" />
% } else {
% }
<input type="submit" class="button" name="SavedSearchSave"   value="<%_('Save')%>" />
% }
<br />
<hr />
<&|/l&>load saved search:</&>
<& SelectSearchesForObjects, name => 'SavedSearchLoad', objects => \@objects, search_type => $type &>
<input type="submit" value="<% _('Load') %>" class="button" />

</&>

<%INIT>
return unless Jifty->web->current_user->has_right(
    right  => 'LoadSavedSearch',
    object => RT->system,
);

my $can_modify = Jifty->web->current_user->has_right(
    right  => 'CreateSavedSearch',
    object => RT->system,
);

use RT::SavedSearches;
my @objects = RT::SavedSearches->new()->_privacy_objects;
push @objects, RT::System->new()
    if Jifty->web->current_user->has_right( object=> RT->system,
                                          right => 'SuperUser' );
my $is_dirty = sub {
    my %arg = (
        Query       => {},
        SavedSearch => {},
        SearchFields => [qw(Query format order_by order rows_per_page)],
        @_
    );

    my $obj  = $arg{'SavedSearch'}->{'object'};
    return 0 unless $obj && $obj->id;

    foreach( @{ $arg{'SearchFields'} } ) {
        return 1 if $obj->sub_value( $_ ) ne $arg{'Query'}->{$_};
    }

    return 0;
};

# If we're modifying an old query, check if it's been changed
my $Dirty = $is_dirty->(
    Query       => $current_search,
    SavedSearch => { id => $id, object => $object, description => $description },
    SearchFields => \@search_fields,
);


</%INIT>

<%ARGS>
$id            => 'new'
$object        => undef
$type          => 'Ticket'
$description   => ''
$current_search => {}
@search_fields   => ()
$allow_copy     => 1
$title         => _('Saved searches')
</%ARGS>

<%METHOD Init>
<%ARGS>
$query       => {}
$saved_search => {}
@search_fields => qw(Query format order_by order rows_per_page)
</%ARGS>
<%INIT>


$saved_search->{'id'}          = $ARGS{'SavedSearchId'}          || 'new';
$saved_search->{'description'} = $ARGS{'saved_search_description'} || undef;
$saved_search->{'Privacy'}     = $ARGS{'SavedSearchOwner'}       || undef;

my @results;

if ( $ARGS{'SavedSearchRevert'} ) {
    $ARGS{'SavedSearchLoad'} = $saved_search->{'id'};
}

if ( $ARGS{'SavedSearchLoad'} ) {
    my ($container, $id ) = _parse_saved_search ($ARGS{'SavedSearchLoad'});
    my $search = $container->attributes->with_id( $id );

    $saved_search->{'id'}          = $ARGS{'SavedSearchLoad'};
    $saved_search->{'object'}      = $search;
    $saved_search->{'description'} = $search->description;
    $query->{$_} = $search->sub_value($_) foreach @search_fields;

    if ( $ARGS{'SavedSearchRevert'} ) {
        push @results, _('Loaded original "%1" saved search', $saved_search->{'description'} );
    } else {
        push @results, _('Loaded saved search "%1"', $saved_search->{'description'} );
    }
}
elsif ( $ARGS{'SavedSearchDelete'} ) {
    # We set $SearchId to 'new' above already, so peek into the %ARGS
    my ($container, $id) = _parse_saved_search( $saved_search->{'id'} );
    if ( $container && $container->id ) {
        # We have the object the entry is an attribute on; delete the entry...
        $container->attributes->delete_entry( name => 'SavedSearch', id => $id );
    }
    $saved_search->{'id'}          = 'new';
    $saved_search->{'object'}      = undef;
    $saved_search->{'description'} = undef;
    push @results, _("Deleted saved search");
}
elsif ( $ARGS{'SavedSearchCopy'} ) {
    my ($container, $id ) = _parse_saved_search( $ARGS{'SavedSearchid'} );
    $saved_search->{'object'} = $container->attributes->withid( $id );
    if ( $ARGS{'saved_search_description'} && $ARGS{'saved_search_description'} ne $saved_search->{'object'}->description ) {
        $saved_search->{'description'} = $ARGS{'saved_search_description'};
    } else {
        $saved_search->{'description'} = _( "%1 copy", $saved_search->{'object'}->description );
    }
    $saved_search->{'id'}          = 'new';
    $saved_search->{'object'}      = undef;
}

if ( $saved_search->{'id'} && $saved_search->{'id'} ne 'new'
     && !$saved_search->{'object'} )
{
    my ($container, $id ) = _parse_saved_search( $ARGS{'SavedSearchId'} );
    $saved_search->{'object'} = $container->attributes->with_id( $id );
    $saved_search->{'description'} ||= $saved_search->{'object'}->description;
}

return @results;

</%INIT>
</%METHOD>

<%METHOD Save>
<%ARGS>
$query        => {}
$saved_search  => {}
@search_fields => qw(Query format order_by order rows_per_page)
</%ARGS>
<%INIT>

return unless $ARGS{'SavedSearchSave'} || $ARGS{'SavedSearchCopy'};

my @results;
my $obj  = $saved_search->{'object'};
my $id   = $saved_search->{'id'};
my $desc = $saved_search->{'description'};

my $privacy = $saved_search->{'Privacy'};

my %params = map { $_ => $query->{$_} } @search_fields;
my ($new_obj_type, $new_obj_id) = split(/\-/, ($privacy || ''));

if ( $obj && $obj->id ) {
    # permission check
    if ($obj->object->isa('RT::System')) {
        unless (Jifty->web->current_user->has_right( object=> RT->system, right => 'SuperUser')) {
            push @results, _("No permission to save system-wide searches");
            return @results;
        }
    }

    $obj->set_sub_values( %params );
    $obj->set_description( $desc );

    my $obj_type = ref($obj->object);
    # We need to get current obj_id now, because when we change obj_type to
    # RT::System, $obj->object->id returns 1, not the old one :(
    my $obj_id = $obj->object->id;

    if ( $new_obj_type && $new_obj_id ) {
        my ($val, $msg);
        if ( $new_obj_type ne $obj_type ) {
            ($val, $msg ) = $obj->set_objectType($new_obj_type);
            push @results, _('Unable to set privacy object: %1', $msg) unless ( $val );
        }
        if ( $new_obj_id != $obj_id ) {
            ($val, $msg) = $obj->set_objectid($new_obj_id);
            push @results, _('Unable to set privacy id: %1', $msg) unless ( $val );
        }
    } else {
        push @results, _('Unable to determine object type or id');
    }
    push @results, _('Updated saved search "%1"', $desc);
}
elsif ( $id eq 'new' ) {
    my $saved_search = RT::SavedSearch->new();
    my ($status, $msg) = $saved_search->save(
        privacy      => $privacy,
        name         => $desc,
        type         => $saved_search->{'type'},
        search_params => \%params,
    );

    if ( $status ) {
        $saved_search->{'object'} =
            Jifty->web->current_user->user_object->attributes->with_id( $saved_search->id );
        # Build new SearchId
        $saved_search->{'id'} =
                ref( Jifty->web->current_user->user_object ) . '-'
                    . Jifty->web->current_user->user_object->id
                    . '-SavedSearch-'
                    . $saved_search->{'object'}->id;
    }
    else {
        push @results, _("Can't find a saved search to work with").': '._($msg);
    }
}
else {
    push @results, _("Can't save this search");
}

return @results;

</%INIT>
</%METHOD>
