%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%# 
%# This software is Copyright (c) 1996-2008 Best Practical Solutions, LLC
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
%# REST/1.0/Forms/ticket/default
%#
<%ARGS>
$id
$changes => {}
$fields => undef
$args => undef
</%ARGS>
<%INIT>
use MIME::Entity;
use RT::Interface::REST;

my $cf_spec = RT::Interface::REST->custom_field_spec(1);

my @comments;
my ($c, $o, $k, $e) = ("", [], {}, 0);
my %data   = %$changes;
my $ticket = RT::Model::Ticket->new() ;
my @dates  = qw(created starts started due resolved told last_updated);
my @people = qw(requestors cc admin_cc);
my @create = qw(queue requestor subject cc admin_cc owner status priority
                initial_priority final_priority time_estimated time_worked
                time_left starts started due resolved);
my @simple = qw(subject status priority disabled time_estimated time_worked
                time_left initial_priority final_priority);
my %dates  = map {lc $_ => $_} @dates;
my %people = map {lc $_ => $_} @people;
my %create = map {lc $_ => $_} @create;
my %simple = map {lc $_ => $_} @simple;

# Are we dealing with an existing ticket?
if ($id ne 'new') {
    $ticket->load($id);
    if (!$ticket->id) {
        return [ "# Ticket $id does not exist.", [], {}, 1 ];
    }
    elsif (!$ticket->current_user_has_right('ShowTicket') ||
           (%data && !$ticket->current_user_has_right('ModifyTicket')))
    {
        my $act = %data ? "modify" : "display";
        return [ "# You are not allowed to $act ticket $id.", [], {}, 1 ];
    }
}
else {
    if (!keys(%data)) {
        # GET ticket/new: Return a suitable default form.
        # We get defaults from queue/1 (XXX: What if it isn't there?).
        my $due = RT::Date->new() ;
        my $queue = RT::Model::Queue->new() ;
        my $starts = RT::Date->new() ;
        $queue->load(1);
        $due->set_to_now;
        $due->add_days($queue->default_due_in) if $queue->default_due_in;
        $starts->set_to_now;

        return [
            "# Required: id, Queue",
            [ qw(id queue requestor subject cc AdminCc Owner status priority
                 initial_priority final_priority time_estimated starts Due Text) ],
            {
                id               => "ticket/new",
                queue            => $queue->name,
                Requestor        => Jifty->web->current_user->name,
                subject          => "",
                cc               => [],
                admin_cc          => [],
                owner            => "",
                status           => "new",
                priority         => $queue->initial_priority,
                initial_priority  => $queue->initial_priority,
                final_priority    => $queue->final_priority,
                time_estimated    => 0,
                starts           => $starts->iso,
                due              => $due->iso,
                Text             => "",
            },
            0
        ];
    }
    else {
        # We'll create a new ticket, and fall through to set fields that
        # can't be set in the call to Create().
        my (%v, $text);

        foreach my $k (keys %data) {
            # flexibly parse any dates
            if ($dates{lc $k}) {
                my $time = new RT::Date Jifty->web->current_user;
                $time->set(format => 'unknown', value => $data{$k});
                $data{$k} = $time->ISO;
            }

            if (exists $create{lc $k}) {
                $v{$create{lc $k}} = delete $data{$k};
            }
            # Set custom field
            elsif ($k =~ /^$cf_spec/) {
                my $cf = RT::Model::CustomField->new(current_user => RT->system_user );
                my $cfk = $1 || $2;
                unless($cf->load_by_name( name => $cfk )) {
                    push @comments, "# Invalid custom field name ($cfk)";
                    delete $data{$k};
                    next;
                }
                $v{"CustomField-".$cf->id()} = delete $data{$k};
            }
            elsif (lc $k eq 'text') {
                $text = delete $data{$k};
            }
        }

        # people fields allow multiple values
        $v{$_} = vsplit($v{$_}) foreach ( grep $create{lc $_}, @people );

        if ($text) {
            $v{mime_obj} =
                MIME::Entity->build(
                    From => Jifty->web->current_user->email,
                    subject => $v{subject},
                    Data => $text
                );
        }

        my($tid,$trid,$terr) = $ticket->create(%v);    
        unless ($tid) {
            push(@comments, "# Could not create ticket.");
            push(@comments, "# " . $terr);
            goto DONE;
        }

        delete $data{id};
        $id = $ticket->id;
        push(@comments, "# Ticket $id created.");
        # see if the hash is empty
        goto DONE if ! keys(%data);
    }
}

# Now we know we're dealing with an existing ticket.
if (!keys(%data)) {
    my ($time, $key, $val, @data);

    push @data, [ id    => "ticket/".$ticket->id   ];
    push @data, [ Queue => $ticket->queue_obj->name ] 
	if (!%$fields || exists $fields->{lc 'Queue'});
    push @data, [ Owner => $ticket->owner_obj->name ]
	if (!%$fields || exists $fields->{lc 'Owner'});
    push @data, [ Creator => $ticket->creator_obj->name ]
	if (!%$fields || exists $fields->{lc 'Creator'});

    foreach (qw(subject status priority initial_priority final_priority)) {
	next unless (!%$fields || (exists $fields->{lc $_}));
        push @data, [ucfirst $_ => $ticket->$_ ];
    }

    foreach $key (@people) {
        next unless (!%$fields || (exists $fields->{lc $key}));
        
        push @data, [ ucfirst $key => [ $ticket->role_group($key eq 'requestors' ? 'requestor' : $key)->member_emails ] ];
    }

    $time = RT::Date->new();
    foreach $key (@dates) {
	next unless (!%$fields || (exists $fields->{lc $key}));
        $time->set(format => 'sql', value => $ticket->$key);
        push @data, [ ucfirst $key => $time->as_string ];
    }

    $time = RT::Date->new();
    foreach $key (qw(time_estimated time_worked time_left)) {
	next unless (!%$fields || (exists $fields->{lc $key}));
        $val = $ticket->$key || 0;
        $val = "$val minutes" if $val;
        push @data, [ ucfirst $key => $val ];
    }

    # Display custom fields
    my $CustomFields = $ticket->queue_obj->ticket_custom_fields();
    while (my $cf = $CustomFields->next()) {
        next unless !%$fields
                 || exists $fields->{"cf.{".lc($cf->name)."}"}
                 || exists $fields->{"cf-".lc $cf->name};

        my $vals = $ticket->custom_field_values($cf->id());
        my @out = ();
        while (my $v = $vals->next()) {
            push @out, $v->content;
        }
        push @data, [ ('CF.{' . $cf->name . '}') => join ',', @out ];
    }

    my %k = map {@$_} @data;
    $o = [ map {$_->[0]} @data ];
    $k = \%k;
}
else {
    my ($get, $set, $key, $val, $n, $s);
    delete $data{id};
    foreach $key (keys %data) {

        $val = $data{$key};
        $key = lc $key;
        $n = 1;

        if (ref $val eq 'ARRAY') {
            unless ($key =~ /^(?:requestors|cc|admin_cc)$/i) {
                $n = 0;
                $s = "$key may have only one value.";
                goto SET;
            }
        }

        if ($key =~ /^queue$/i) {
            next if $val eq $ticket->queue_obj->name;
            ($n, $s) = $ticket->set_queue($val);
        }
        elsif ($key =~ /^owner$/i) {
            next if $val eq $ticket->owner_obj->name;
            ($n, $s) = $ticket->set_owner($val);
        }
        elsif (exists $simple{$key}) {
            $key = $simple{$key};
            $set = "set_$key";

            next if (($val eq $ticket->$key)|| ($ticket->$key =~ /^\d+$/ && $val == $ticket->$key));
            ($n, $s) = $ticket->$set("$val");
        }
        elsif (exists $dates{$key}) {
            $key = $dates{$key};
            $set = "set_$key";

            my $time = RT::Date->new() ;
            $time->set(format => 'sql', value => $ticket->$key);
            next if ($val =~ /^not set$/i || $val eq $time->as_string);

            $time->set(format => 'unknown', value => $val);
            ($n, $s) = $ticket->$set($time->ISO);
        }
        elsif (exists $people{$key}) {
            $key = $people{$key};
            my ($p, @msgs);
            my $type = $key eq 'requestors' ? 'requestor' : $key;

            my %new  = map {$_=>1} @{ vsplit($val) };
            my %old  = map {$_=>1} $ticket->role_group($type)->member_emails;

            foreach $p (keys %old) {
                unless (exists $new{$p}) {
                    ($s, $n) = $ticket->delete_watcher(type => $type,
                                                      email => $p);
                    push @msgs, [ $s, $n ];
                }
            }
            foreach $p (keys %new) {
                # XXX: This is a stupid test.
                unless ($p =~ /^[\w.+-]+\@([\w.-]+\.)*\w+.?$/) {
                    $s = 0;
                    $n = "$p is not a valid email address.";
                    push @msgs, [ $s, $n ];
                    next;
                }
                unless ($ticket->is_watcher(type => $type, email => $p)) {
                    ($s, $n) = $ticket->add_watcher(type => $type,
                                                   email => $p);
                    push @msgs, [ $s, $n ];
                }
            }

            $n = 1;
            if (@msgs = grep {$_->[0] == 0} @msgs) {
                $n = 0;
                $s = join "\n", map {"# ".$_->[1]} @msgs;
                $s =~ s/^# //;
            }
        }
        # Set custom field
        elsif ($key =~ /^$cf_spec/) {
            my $cf = RT::Model::CustomField->new(current_user => RT->system_user );
            $key = $1 || $2;
            if (not $cf->load_by_name( name => $key )) {
                $n = 0;
                $s = "Unknown custom field.";
            }
            else {
                ($n, $s) = $ticket->add_custom_field_value(
                             column => $cf, value => $val );
                $s =~ s/^# // if defined $s;
            }
        }
        elsif ($key ne 'id' && $key ne 'type' && $key ne 'creator') {
            $n = 0;
            $s = "Unknown field.";
        }
    SET:
        if ($n == 0) {
            $e = 1;
            push @comments, "# $key: $s";
            unless (@$o) {
                my %o = keys %$changes;
                delete $o{id};
                @$o = ("id", keys %o);
                $k = $changes;
            }
        }
    }
    push(@comments, "# Ticket ".$ticket->id." updated.") unless $n == 0;
}

DONE:
$c ||= join("\n", @comments) if @comments;
return [$c, $o, $k, $e];

</%INIT>
