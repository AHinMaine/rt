#!/usr/bin/env perl
# BEGIN BPS TAGGED BLOCK {{{
# 
# COPYRIGHT:
#  
# This software is Copyright (c) 1996-2007 Best Practical Solutions, LLC 
#                                          <jesse@bestpractical.com>
# 
# (Except where explicitly superseded by other copyright notices)
# 
# 
# LICENSE:
# 
# This work is made available to you under the terms of Version 2 of
# the GNU General Public License. A copy of that license should have
# been provided with this software, but in any event can be snarfed
# from www.gnu.org.
# 
# This work is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
# 02110-1301 or visit their web page on the internet at
# http://www.gnu.org/copyleft/gpl.html.
# 
# 
# CONTRIBUTION SUBMISSION POLICY:
# 
# (The following paragraph is not intended to limit the rights granted
# to you to modify and distribute this software under the terms of
# the GNU General Public License and is only of importance to you if
# you choose to contribute your changes and enhancements to the
# community by submitting them to Best Practical Solutions, LLC.)
# 
# By intentionally submitting any modifications, corrections or
# derivatives to this work, or any other work intended for use with
# Request Tracker, to Best Practical Solutions, LLC, you confirm that
# you are the copyright holder for those contributions and you grant
# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
# royalty-free, perpetual, license to use, copy, create derivative
# works based on those contributions, and sublicense and distribute
# those contributions and any derivatives thereof.
# 
# END BPS TAGGED BLOCK }}}
use strict;
use Carp;

use lib ("/home/jesse/svk/3.999-DANGEROUS/local/lib", "/home/jesse/svk/3.999-DANGEROUS/lib");

use RT;

use Getopt::Long;

BEGIN { RT::init_jifty(); }

use RT::Interface::CLI qw(clean_env get_current_user get_message_content );
use RT::Model::TicketCollection;
use RT::Model::Template;

#Clean out all the nasties from the environment
clean_env();

# Load the config file
RT::load_config();

#Connect to the database and get RT->system_user and RT::nobody loaded
RT::init();

#Get the current user all loaded
my $CurrentUser = get_current_user();

unless ( $CurrentUser->id ) {
    print _("No RT user found. Please consult your RT administrator.\n");
    exit(1);
}

my ( $search, $condition, $action, $search_arg, $condition_arg, $action_arg,
     $template_id, $transaction, $transaction_type, $help, $verbose );
GetOptions( "search=s"           => \$search,
            "search-arg=s"       => \$search_arg,
            "condition=s"        => \$condition,
            "condition-arg=s"    => \$condition_arg,
            "action-arg=s"       => \$action_arg,
            "action=s"           => \$action,
            "template-id=s"      => \$template_id,
            "transaction=s"      => \$transaction,
            "transaction-type=s" => \$transaction_type,
            "help"               => \$help,
            "verbose|v"          => \$verbose );

help() if $help or not $search or not $action;

$transaction = lc( $transaction||'' );
if ( $transaction && $transaction !~ /^(first|all|last)$/i ) {
    print STDERR _("--transaction argument could be only 'first', 'last' or 'all'");
    exit 1;
}

# We _must_ have a search object
load_module($search);
load_module($action)    if ($action);
load_module($condition) if ($condition);

# load template if specified
my $template_obj;
if ($template_id) {
    $template_obj = RT::Model::Template->new( current_user => $CurrentUser );
    $template_obj->load($template_id);
}
my $void_scrip = RT::Model::Scrip->new( current_user => $CurrentUser );
my $void_scrip_action = RT::Model::ScripAction->new( current_user => $CurrentUser );

#At the appointed time:

#find a bunch of tickets
my $tickets = RT::Model::TicketCollection->new( current_user => $CurrentUser );
my $search  = $search->new(
    tickets_obj  => $tickets,
    argument    => $search_arg,
    current_user => $CurrentUser
);

$search->prepare();

# TicketsFound is an RT::Model::TicketCollection object
my $tickets = $search->tickets_obj;

#for each ticket we've found
while ( my $ticket = $tickets->next() ) {
    print $ticket->id() . ": " if ($verbose);

    if ( $transaction ) {
        my $txns = get_transactions($ticket);
        my $found = 0;
        while ( my $txn = $txns->Next ) {
            print _("Using transaction #[_1]...", $txn->id)
                if $verbose;
            process($ticket, $txn);
            $found = 1;
        }
        print _("Couldn't find suitable transaction, skipping")
            if $verbose && !$found;
    } else {
        print _("Processing without transaction, some conditions and actions may fail. Consider using --transaction argument")
            if $verbose;

        process($ticket);
    }
}

sub process {
    my $ticket = shift;
    my $transaction = shift;

    # perform some more advanced check
    if ($condition) {
        my $condition_obj = $condition->new(
            transaction_obj => $transaction,
            ticket_obj      => $ticket,
            scrip_obj       => $void_scrip,
            template_obj    => $template_obj,
            argument       => $condition_arg,
            current_user    => $CurrentUser,
        );

        # if the condition doesn't apply, get out of here

        return unless $condition_obj->is_applicable;
        print _("Condition matches...") if $verbose;
    }

    #prepare our action
    my $action_obj = $action->new(
        ticket_obj      => $ticket,
        transaction_obj => $transaction,
        template_obj    => $template_obj,
        argument       => $action_arg,
        scrip_obj       => $void_scrip,
        scrip_action_obj => $void_scrip_action,
        current_user    => $CurrentUser,
    );

    #if our preparation, move onto the next ticket
    next unless $action_obj->prepare;
    print _("Action prepared...") if $verbose;

    #commit our action.
    next unless $action_obj->commit;
    print _("Action committed.\n") if $verbose;
}

=head2 get_transactions

Takes ticket and returns L<RT::Model:;TransactionCollection> object 
with transactions of the ticket according to command line arguments
C<--transaction> and <--transaction-type>.

=cut

sub get_transactions {
    my $ticket = shift;
    my $txns = $ticket->transactions;
    my $order = $transaction eq 'last'? 'DESC': 'ASC';
    $txns->order_by(
        { column => 'created', order => $order },
        { column => 'id', order => $order },
    );
    if ( $transaction_type ) {
        $transaction_type =~ s/^\s+//;
        $transaction_type =~ s/\s+$//;
        foreach my $type ( split /\s*,\s*/, $transaction_type ) {
            $txns->limit( column => 'type', value => $type, entry_aggregator => 'OR' );
        }
    }
    $txns->rows_per_page(1) unless $transaction eq 'all';
    return $txns;
}

# {{{ load_module 

=head2 load_module

Loads a perl module, dying nicely if it can't find it.

=cut

sub load_module {
    my $modname = shift;
    eval "require $modname";
    if ($@) {
        die _( "Failed to load module %1. (%2)", $modname, $@ );
    }

}

# }}}


sub help {

    print _( "%1 is a tool to act on tickets from an external scheduling tool, such as cron.", $0 )
      . "\n";
    print _("It takes several arguments:") . "\n\n";

    print "	"
      . _( "%1 - Specify the search module you want to use", "--search" )
      . "\n";
    print "	"
      . _( "%1 - An argument to pass to %2", "--search-arg", "--search" )
      . "\n";

    print "	"
      . _( "%1 - Specify the condition module you want to use", "--condition" )
      . "\n";
    print "	"
      . _( "%1 - An argument to pass to %2", "--condition-arg", "--condition" )
      . "\n";
    print "	"
      . _( "%1 - Specify the action module you want to use", "--action" )
      . "\n";
    print "	"
      . _( "%1 - An argument to pass to %2", "--action-arg", "--action" )
      . "\n";
    print "	"
      . _( "%1 - Specify id of the template you want to use", "--template-id" )
      . "\n";
    print "	"
      . _( "%1 - Specify if you want to use either 'first', 'last' or 'all' transaction", "--transaction" )
      . "\n";
    print "	"
      . _( "%1 - Specify the comma separated list of transactions' types you want to use", "--transaction-type" )
      . "\n";
    print "	"
      . _( "%1 - Output status updates to STDOUT", "--verbose" ) . "\n";
    print "\n";
    print "\n";
    print _("Security:")."\n";
    print _("This tool allows the user to run arbitrary perl modules from within RT.")." ". 
        _("If this tool were setgid, a hostile local user could use this tool to gain administrative access to RT.")." ".
        _("It is incredibly important that nonprivileged users not be allowed to run this tool."). " " . 
        _("It is suggested that you create a non-privileged unix user with the correct group membership and RT access to run this tool.")."\n";
    print "\n";
    print _("Example:");
    print "\n";
    print " "
      . _( "The following command will find all active tickets in the queue 'general' and set their priority to 99 if they haven't been touched in 4 hours:"
      )
      . "\n\n";

    print " bin/rt-crontool \\\n";
    print "  --search RT::Search::ActiveTicketsInQueue  --search-arg general \\\n";
    print "  --condition RT::Condition::UntouchedInHours --condition-arg 4 \\\n";
    print "  --action RT::ScripAction::SetPriority --action-arg 99 \\\n";
    print "  --verbose\n";

    print "\n";
    print _("Escalate tickets"). "\n";
    print " bin/rt-crontool \\\n";
    print "  --search RT::Search::ActiveTicketsInQueue  --search-arg general \\\n";
    print "  --action RT::ScripAction::EscalatePriority\n";
 
 
 



    exit(0);
}
