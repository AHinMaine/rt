#!@PERL@ -w
# BEGIN BPS TAGGED BLOCK {{{
# 
# COPYRIGHT:
#  
# This software is Copyright (c) 1996-2007 Best Practical Solutions, LLC 
#                                          <jesse@bestpractical.com>
# 
# (Except where explicitly superseded by other copyright notices)
# 
# 
# LICENSE:
# 
# This work is made available to you under the terms of Version 2 of
# the GNU General Public License. A copy of that license should have
# been provided with this software, but in any event can be snarfed
# from www.gnu.org.
# 
# This work is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
# 02110-1301 or visit their web page on the internet at
# http://www.gnu.org/copyleft/gpl.html.
# 
# 
# CONTRIBUTION SUBMISSION POLICY:
# 
# (The following paragraph is not intended to limit the rights granted
# to you to modify and distribute this software under the terms of
# the GNU General Public License and is only of importance to you if
# you choose to contribute your changes and enhancements to the
# community by submitting them to Best Practical Solutions, LLC.)
# 
# By intentionally submitting any modifications, corrections or
# derivatives to this work, or any other work intended for use with
# Request Tracker, to Best Practical Solutions, LLC, you confirm that
# you are the copyright holder for those contributions and you grant
# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
# royalty-free, perpetual, license to use, copy, create derivative
# works based on those contributions, and sublicense and distribute
# those contributions and any derivatives thereof.
# 
# END BPS TAGGED BLOCK }}}
use strict;

# fix lib paths, some may be relative
BEGIN {
    require File::Spec;
    my @libs = ("@RT_LIB_PATH@", "@LOCAL_LIB_PATH@");
    my $bin_path;

    for my $lib (@libs) {
        unless ( File::Spec->file_name_is_absolute($lib) ) {
            unless ($bin_path) {
                if ( File::Spec->file_name_is_absolute(__FILE__) ) {
                    $bin_path = ( File::Spec->splitpath(__FILE__) )[1];
                }
                else {
                    require FindBin;
                    no warnings "once";
                    $bin_path = $FindBin::Bin;
                }
            }
            $lib = File::Spec->catfile( $bin_path, File::Spec->updir, $lib );
        }
        unshift @INC, $lib;
    }

}

use RT;
use XML::Simple;

RT::load_config();
RT::Init();

my $LocalOnly = @ARGV ? shift(@ARGV) : 1;

my %RV;
my %Ignore = (
    All => [qw(
	id Created creator last_updated last_updated_by
    )],
    Templates => [qw(
	TranslationOf
    )],
);

my $system_userId = RT->system_user->id;
my @classes = qw(
    Users Groups Queues ScripActions ScripConditions
    Templates Scrips ACL CustomFields
);
foreach my $class (@classes) {
    require "RT/$class.pm";
    my $objects = "RT::$class"->new(current_user => RT->system_user);
    $objects->{find_disabled_rows} = 1;
    $objects->find_all_rows;

    if ($class eq 'CustomFields') {
        $objects->order_by(
            { column => 'lookup_type' },
            { column => 'sort_order' },
            { column => 'id' },
        );
    }
    else {
        $objects->order_by( column => 'id' );
    }

    if ($LocalOnly) {
        next if $class eq 'ACL'; # XXX - would go into infinite loop - XXX
	$objects->limit( column => 'last_updated_by', operator => '!=', value => $system_userId )
	    unless $class eq 'Groups';
	$objects->limit( column => 'id', operator => '!=', value => $system_userId )
	    if $class eq 'Users';
	$objects->limit( column => 'domain', operator => '=', value => 'UserDefined' )
	    if $class eq 'Groups';
    }

    my %fields;
    while (my $obj = $objects->next) {
	next if $obj->can('last_updated_by') and $obj->last_updated_by == $system_userId;

	if (!%fields) {
	    %fields = map { $_ => 1 } keys %{$obj->_ClassAccessible};
	    delete @fields{
		@{$Ignore{$class}||=[]},
		@{$Ignore{All}||=[]},
	    };
	}

	my $rv;
	# next if $obj-> # skip default names
	foreach my $field (sort keys %fields) {
	    my $value = $obj->__value($field);
	    $rv->{$field} = $value if length($value);
	}
	delete $rv->{disabled} unless $rv->{disabled};

	foreach my $record (map { /ACL/ ? 'ACE' : substr($_, 0, -1) } @classes) {
	    foreach my $key (map "$record$_", ('', 'id')) {
		next unless exists $rv->{$key};
		my $id = $rv->{$key} or next;
		my $obj = "RT::$record"->new(current_user => RT->system_user);
		$obj->load_by_cols( id => $id ) or next;
		$rv->{$key} = $obj->__value('name') || 0;
	    }
	}

	if ($class eq 'Users' and defined $obj->privileged) {
	    $rv->{privileged} = int($obj->privileged);
	}
	elsif ($class eq 'CustomFields') {
	    my $values = $obj->values;
	    while (my $value = $values->next) {
		push @{$rv->{values}}, {
		    map { ($_ => $value->__value($_)) } qw(
			name description sort_order
		    ),
		};
	    }
	}

	if (eval { require RT::Model::AttributeCollection; 1 }) {
	    my $attributes = $obj->attributes;
	    while (my $attribute = $attributes->next) {
		my $content = $attribute->content;
		$rv->{Attributes}{$attribute->name} = $content if length($content);
	    }
	}

	push @{$RV{$class}}, $rv;
    }
}

print(<< ".");
no strict; use XML::Simple; *_ = XMLin(do { local \$/; readline(DATA) }, ForceArray => [qw(
 @classes values
)], NoAttr => 1, SuppressEmpty => ''); *\$_ = (\$_{\$_} || []) for keys \%_; 1; # vim: ft=xml
__DATA__
.

print XMLout(
    { map { ($_ => ($RV{$_} || [])) } @classes },
    Rootname => 'InitialData',
    NoAttr => 1,
    SuppressEmpty => '',
    XMLDecl => '<?xml version="1.0" encoding="UTF-8"?>',
);
