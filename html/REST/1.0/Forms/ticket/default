%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%#  
%# This software is Copyright (c) 1996-2007 Best Practical Solutions, LLC 
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/copyleft/gpl.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
%# REST/1.0/Forms/ticket/default
%#
<%ARGS>
$id
$changes => {}
$fields => undef
$args => undef
</%ARGS>
<%INIT>
use MIME::Entity;

my @comments;
my ($c, $o, $k, $e) = ("", [], {}, 0);
my %data   = %$changes;
my $ticket = RT::Model::Ticket->new() ;
my @dates  = qw(Created starts Started Due Resolved Told LastUpdated);
my @people = qw(Requestors Cc AdminCc);
my @create = qw(Queue Requestor Subject Cc AdminCc Owner Status Priority
                initial_priority final_priority time_estimated time_worked
                time_left starts Started Due Resolved);
my @simple = qw(Subject Status Priority disabled time_estimated time_worked
                time_left initial_priority final_priority);
my %dates  = map {lc $_ => $_} @dates;
my %people = map {lc $_ => $_} @people;
my %create = map {lc $_ => $_} @create;
my %simple = map {lc $_ => $_} @simple;

# Are we dealing with an existing ticket?
if ($id ne 'new') {
    $ticket->load($id);
    if (!$ticket->id) {
        return [ "# Ticket $id does not exist.", [], {}, 1 ];
    }
    elsif (!$ticket->current_user_has_right('ShowTicket') ||
           (%data && !$ticket->current_user_has_right('ModifyTicket')))
    {
        my $act = %data ? "modify" : "display";
        return [ "# You are not allowed to $act ticket $id.", [], {}, 1 ];
    }
}
else {
    if (!keys(%data)) {
        # GET ticket/new: Return a suitable default form.
        # We get defaults from queue/1 (XXX: What if it isn't there?).
        my $due = RT::Date->new() ;
        my $queue = RT::Model::Queue->new() ;
        my $starts = RT::Date->new() ;
        $queue->load(1);
        $due->set_to_now;
        $due->AddDays($queue->DefaultDueIn) if $queue->DefaultDueIn;
        $starts->set_to_now;

        return [
            "# Required: id, Queue",
            [ qw(id Queue Requestor Subject Cc AdminCc Owner Status Priority
                 initial_priority final_priority time_estimated starts Due Text) ],
            {
                id               => "ticket/new",
                Queue            => $queue->name,
                Requestor        => Jifty->web->current_user->name,
                Subject          => "",
                Cc               => [],
                AdminCc          => [],
                Owner            => "",
                Status           => "new",
                Priority         => $queue->initial_priority,
                initial_priority  => $queue->initial_priority,
                final_priority    => $queue->final_priority,
                time_estimated    => 0,
                starts           => $starts->ISO,
                Due              => $due->ISO,
                Text             => "",
            },
            0
        ];
    }
    else {
        # We'll create a new ticket, and fall through to set fields that
        # can't be set in the call to Create().
        my (%v, $text);

        foreach my $k (keys %data) {
            if (exists $create{lc $k}) {
                $v{$create{lc $k}} = delete $data{$k};
            }
            # Set custom field
            elsif ($k =~ /^CF-/i) {
                my $cf = RT::Model::CustomField->new(current_user => RT->system_user );
                my $cfk = $k;
                $cfk =~ s/^CF-//i;
                unless($cf->load_by_name( name => $cfk )) {
                    push @comments, "# Invalid custom field name ($cfk)";
                    delete $data{$k};
                    next;
                }
                $v{"CustomField-".$cf->id()} = delete $data{$k};
            }
            elsif (lc $k eq 'text') {
                $text = delete $data{$k};
            }
        }

        # people fields allow multiple values
        $v{$_} = vsplit($v{$_}) foreach ( grep $create{lc $_}, @people );

        if ($text) {
            $v{MIMEObj} =
                MIME::Entity->build(
                    From => Jifty->web->current_user->email,
                    Subject => $v{Subject},
                    Data => $text
                );
        }

        my($tid,$trid,$terr) = $ticket->create(%v);    
        unless ($tid) {
            push(@comments, "# Could not create ticket.");
            push(@comments, "# " . $terr);
            goto DONE;
        }

        delete $data{id};
        $id = $ticket->id;
        push(@comments, "# Ticket $id Created.");
        # see if the hash is empty
        goto DONE if ! keys(%data);
    }
}

# Now we know we're dealing with an existing ticket.
if (!keys(%data)) {
    my ($time, $key, $val, @data);

    push @data, [ id    => "ticket/".$ticket->id   ];
    push @data, [ Queue => $ticket->queue_obj->name ] 
	if (!%$fields || exists $fields->{lc 'Queue'});
    push @data, [ Owner => $ticket->OwnerObj->name ]
	if (!%$fields || exists $fields->{lc 'Owner'});
    push @data, [ Creator => $ticket->creator_obj->name ]
	if (!%$fields || exists $fields->{lc 'Creator'});

    foreach (qw(Subject Status Priority initial_priority final_priority)) {
	next unless (!%$fields || (exists $fields->{lc $_}));
        push @data, [$_ => $ticket->$_ ];
    }

    foreach $key (@people) {
        next unless (!%$fields || (exists $fields->{lc $key}));
        push @data, [ $key => [ $ticket->$key->member_emails ] ];
    }

    $time = RT::Date->new();
    foreach $key (@dates) {
	next unless (!%$fields || (exists $fields->{lc $key}));
        $time->set(Format => 'sql', value => $ticket->$key);
        push @data, [ $key => $time->AsString ];
    }

    $time = RT::Date->new();
    foreach $key (qw(time_estimated time_worked time_left)) {
	next unless (!%$fields || (exists $fields->{lc $key}));
        $val = $ticket->$key || 0;
        $val = "$val minutes" if $val;
        push @data, [ $key => $val ];
    }

    # Display custom fields
    my $CustomFields = $ticket->queue_obj->TicketCustomFields();
    while (my $cf = $CustomFields->next()) {
	next unless (!%$fields || (exists $fields->{"cf-".lc $cf->name}));
        my $vals = $ticket->CustomFieldValues($cf->id());
        my @out = ();
        while (my $v = $vals->next()) {
            push @out, $v->Content;
        }
        push @data, [ 'CF-' . $cf->name => join ',', @out ];
    }

    my %k = map {@$_} @data;
    $o = [ map {$_->[0]} @data ];
    $k = \%k;
}
else {
    my ($get, $set, $key, $val, $n, $s);
    delete $data{id};
    foreach $key (keys %data) {

        $val = $data{$key};
        $key = lc $key;
        $n = 1;

        if (ref $val eq 'ARRAY') {
            unless ($key =~ /^(?:Requestors|Cc|AdminCc)$/i) {
                $n = 0;
                $s = "$key may have only one value.";
                goto SET;
            }
        }

        if ($key =~ /^queue$/i) {
            next if $val eq $ticket->queue_obj->name;
            ($n, $s) = $ticket->set_Queue($val);
        }
        elsif ($key =~ /^owner$/i) {
            next if $val eq $ticket->OwnerObj->name;
            ($n, $s) = $ticket->set_Owner($val);
        }
        elsif (exists $simple{$key}) {
            $key = $simple{$key};
            $set = "set_$key";

            next if (($val eq $ticket->$key)|| ($ticket->$key =~ /^\d+$/ && $val == $ticket->$key));
            ($n, $s) = $ticket->$set("$val");
        }
        elsif (exists $dates{$key}) {
            $key = $dates{$key};
            $set = "set_$key";

            my $time = RT::Date->new() ;
            $time->set(Format => 'sql', value => $ticket->$key);
            next if ($val =~ /^not set$/i || $val eq $time->AsString);
            ($n, $s) = $ticket->$set($val);
        }
        elsif (exists $people{$key}) {
            $key = $people{$key};
            my ($p, @msgs);

            my %new  = map {$_=>1} @{ vsplit($val) };
            my %old  = map {$_=>1} $ticket->$key->member_emails;
            my $type = $key eq 'Requestors' ? 'Requestor' : $key;

            foreach $p (keys %old) {
                unless (exists $new{$p}) {
                    ($s, $n) = $ticket->deleteWatcher(Type => $type,
                                                      Email => $p);
                    push @msgs, [ $s, $n ];
                }
            }
            foreach $p (keys %new) {
                # XXX: This is a stupid test.
                unless ($p =~ /^[\w.+-]+\@([\w.-]+\.)*\w+.?$/) {
                    $s = 0;
                    $n = "$p is not a valid email address.";
                    push @msgs, [ $s, $n ];
                    next;
                }
                unless ($ticket->IsWatcher(Type => $type, Email => $p)) {
                    ($s, $n) = $ticket->AddWatcher(Type => $type,
                                                   Email => $p);
                    push @msgs, [ $s, $n ];
                }
            }

            $n = 1;
            if (@msgs = grep {$_->[0] == 0} @msgs) {
                $n = 0;
                $s = join "\n", map {"# ".$_->[1]} @msgs;
                $s =~ s/^# //;
            }
        }
        # Set custom field
        elsif ($key =~ /^CF-/i) {
            my $cf = RT::Model::CustomField->new(current_user => RT->system_user );
            $key =~ s/^CF-//i;
            if (not $cf->load_by_name( name => $key )) {
                $n = 0;
                $s = "Unknown custom field.";
            }
            else {
                ($n, $s) = $ticket->AddCustomFieldValue(
                             column => $cf, value => $val );
                $s =~ s/^# // if defined $s;
            }
        }
        elsif ($key ne 'id' && $key ne 'type' && $key ne 'creator') {
            $n = 0;
            $s = "Unknown field.";
        }
    SET:
        if ($n == 0) {
            $e = 1;
            push @comments, "# $key: $s";
            unless (@$o) {
                my %o = keys %$changes;
                delete $o{id};
                @$o = ("id", keys %o);
                $k = $changes;
            }
        }
    }
    push(@comments, "# Ticket ".$ticket->id." updated.") unless $n == 0;
}

DONE:
$c ||= join("\n", @comments) if @comments;
return [$c, $o, $k, $e];

</%INIT>
