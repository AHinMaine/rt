%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%#  
%# This software is Copyright (c) 1996-2007 Best Practical Solutions, LLC 
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/copyleft/gpl.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
<& /Elements/Header, 
    Title => _("#%1: %2", $ticket_obj->id, $ticket_obj->subject) &>
<& /Ticket/Elements/Tabs, 
    Ticket => $ticket_obj, 
    current_tab => 'Ticket/Display.html?id='.$ticket_obj->id,
    Title => _("#%1: %2", $ticket_obj->id, $ticket_obj->subject) &>

% $m->callback(Callbackname => 'BeforeActionList', %ARGS, Actions => \@Actions, ARGSRef => \%ARGS, Ticket => $ticket_obj);

<& /Elements/ListActions, actions => \@Actions &>
<& Elements/ShowUpdateStatus, Ticket => $ticket_obj &>

% $m->callback( %ARGS, Ticket => $ticket_obj, Callbackname => 'BeforeShowSummary' );
<div class="summary">
<&| /Widgets/TitleBox, title => _('Ticket metadata') &>
<& /Ticket/Elements/ShowSummary,  Ticket => $ticket_obj, Attachments => $attachments &>
</&>
</div>
<br />

% $m->callback( Ticket => $ticket_obj, %ARGS, Callbackname => 'BeforeShowHistory' );

<& /Ticket/Elements/ShowHistory , 
      Ticket => $ticket_obj, 
      Tickets => $Tickets, 
      Collapsed => $ARGS{'Collapsed'}, 
      show_headers => $ARGS{'show_headers'},
      Attachments => $attachments,
      AttachmentContent => $attachment_content
&> 

% $m->callback( %ARGS,
%     Ticket       => $ticket_obj,
%     current_tab  => 'Ticket/Display.html?id=' . $ticket_obj->id,
%     Callbackname => 'AfterShowHistory',
% );

<%ARGS>
$id => undef
$ticket_obj => undef
$show_headers => 0
$Collapsed => undef
</%ARGS>

<%INIT>

$m->callback( ticket_obj => $ticket_obj, ARGSRef => \%ARGS, Callbackname => 'Initial' );

my (@Actions, $Tickets);  


unless ($id || $ticket_obj) {
    abort('No ticket specified');
}

if ($ARGS{'id'} eq 'new') {
    # {{{ Create a new ticket

    my $Queue = RT::Model::Queue->new();
    $Queue->load($ARGS{'queue'});
    unless ( $Queue->id ) {
        abort('Queue not found');
    }

    unless ( $Queue->current_user_has_right('CreateTicket') ) {
        abort('You have no permission to create tickets in that queue.');
    }

    ($ticket_obj, @Actions) = create_ticket(
        Attachments => delete $session{'Attachments'},
        %ARGS,
    );
    unless ( $ticket_obj->current_user_has_right('ShowTicket') ) {
        abort("No permission to view newly Created ticket #".$ticket_obj->id.".");
    }
    # }}}
} else { 
    $ticket_obj ||= load_ticket($ARGS{'id'});

    $m->callback( Callbackname => 'BeforeProcessArguments',
        ticket_obj => $ticket_obj, Tickets => $Tickets,
        ActionsRef => \@Actions, ARGSRef => \%ARGS );
    if ( defined $ARGS{'Action'} ) {
        if ($ARGS{'Action'} =~ /^(Steal|Kill|Take|SetTold)$/) {
            my $action = $1;
            my ($res, $msg) = $ticket_obj->$action();
            push(@Actions, $msg);
        }
    }

    $m->callback(Callbackname => 'ProcessArguments', 
            Ticket => $ticket_obj, 
            ARGSRef => \%ARGS, 
            Actions => \@Actions);
    
    $ARGS{'UpdateContent'} =~ s/\r\n/\n/g if defined $ARGS{'UpdateContent'};
    my $signature = Jifty->web->current_user->user_object->signature || '';
    if ( $ARGS{'UpdateTimeWorked'} || (
        defined $ARGS{'UpdateContent'}
        && $ARGS{'UpdateContent'} ne ''
        && $ARGS{'UpdateContent'} ne "-- \n" . $signature ))
    {
        $ARGS{UpdateAttachments} = $session{'Attachments'};
        push @Actions, process_update_message(
            ARGSRef   => \%ARGS,
            Actions   => \@Actions,
            ticket_obj => $ticket_obj,
        );
        delete $session{'Attachments'};
    }
    push @Actions, process_ticket_watchers(ARGSRef => \%ARGS, ticket_obj => $ticket_obj );
    push @Actions, process_ticket_basics(   ARGSRef => \%ARGS, ticket_obj => $ticket_obj );
    push @Actions, process_ticket_links(    ARGSRef => \%ARGS, ticket_obj => $ticket_obj );

    # XXX: we shouldn't block actions here if user has no right to see the ticket,
    # but we should allow him to see actions he has done
    unless ($ticket_obj->current_user_has_right('ShowTicket')) {
        abort("No permission to view ticket");
    }
    if ( $ARGS{'MarkAsSeen'} ) {
        $ticket_obj->set_attribute(
            name => 'User-'. $ticket_obj->current_user->id .'-SeenUpTo',
            content => $ticket_obj->last_updated,
        );
        push @Actions, _('Marked all messages as seen');
    }
}

$m->callback(
    Callbackname => 'BeforeDisplay',
    ticket_obj => \$ticket_obj,
    Tickets => \$Tickets,
    Actions => \@Actions,
    ARGSRef => \%ARGS,
);

# This code does automatic redirection if any updates happen. 

if (@Actions) {
    # We've done something, so we need to clear the decks to avoid 
    # resubmission on refresh.
    # But we need to store Actions somewhere too, so we don't lose them.
    push @{ $session{"Actions"} ||= [] }, @Actions;
    $session{'i'}++;

    RT::Interface::Web::redirect(RT->config->get('WebURL') ."Ticket/Display.html?id=".$ticket_obj->id);
} else {
    @Actions = @{ delete $session{"Actions"} || [] };
}

my $attachments = $m->comp('Elements/FindAttachments', Ticket => $ticket_obj, Tickets => $Tickets);
my $attachment_content = $m->comp('Elements/LoadTextAttachments', Ticket => $ticket_obj);

</%INIT>
