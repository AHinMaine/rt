<table>
<td><% _('Sign')%></td>
<td><& /Widgets/Form/Boolean:InputOnly, name => 'Sign', current_value => $self->{'Sign'} &>
using <& SelectKeyForSigning, User => Jifty->web->current_user->user_object &>
</td>
<td><% _('Encrypt')%></td>
<td><& /Widgets/Form/Boolean:InputOnly, name => 'Encrypt', current_value => $self->{'Encrypt'} &></td>
</table>
<%ARGS>
$self => undef,
</%ARGS>
<%INIT>
return unless $self;
</%INIT>

<%METHOD new>
<%ARGS>
$Arguments => {}
</%ARGS>
<%INIT>
return undef unless RT->config->get('GnuPG')->{'Enable'};

require RT::Crypt::GnuPG;
return { %$Arguments };
</%INIT>
</%METHOD>

<%METHOD ShowIssues>
<%ARGS>
$self => undef,
</%ARGS>
<%INIT>
return unless $self;

return $m->comp( '/Elements/GnuPGKeyIssues',
    Issues => $self->{'GnuPGRecipientsKeyIssues'} || [],
    SignAddresses => $self->{'GnuPGCanNotSignAs'} || [],
);
</%INIT>
</%METHOD>


<%METHOD Process>
<%ARGS>
$self => undef
$queue_obj => undef
$ticket_obj => undef
</%ARGS>
<%INIT>
return unless $self;

$queue_obj ||= $ticket_obj->queue_obj if $ticket_obj;

foreach ( qw(Sign Encrypt) ) {
    $self->{ $_ } = $m->comp( '/Widgets/Form/Boolean:Process',
        name => $_,
        DefaultValue => $queue_obj->$_,
        Arguments => $self,
    );
}
</%INIT>
</%METHOD>

<%METHOD Check>
<%ARGS>
$self      => undef
$Operation => 'Update'
$ticket_obj => undef
$queue_obj  => undef
</%ARGS>
<%INIT>
return 1 unless $self;

my $checks_failure = 0;

if ( $self->{'Sign'} ) {
    $queue_obj ||= $ticket_obj->queue_obj
        if $ticket_obj;

    my $address = $self->{'SignUsing'};
    $address ||= ($self->{'UpdateType'}||'') eq "private"
        ? ( $queue_obj->comment_address || RT->config->get('comment_address') )
        : ( $queue_obj->correspond_address || RT->config->get('correspond_address') );

    unless ( RT::Crypt::GnuPG::dry_sign( $address ) ) {
        push @{ $self->{'GnuPGCanNotSignAs'} ||= [] }, $address;
        $checks_failure = 1;
    } else {
        RT::Crypt::GnuPG::use_key_for_signing( $self->{'SignUsing'} )
            if $self->{'SignUsing'};
    }
}

if ( $self->{'Encrypt'} ) {

    my @recipients;

    if ( $Operation eq 'Update' ) {
        # skip any email addresses that we won't be sending mail to
        my %squelch = $m->comp(
            '/Ticket/Elements/PreviewScrips:SquelchRecipients',
            %$self,
            ticket_obj => $ticket_obj
        );

        @recipients = $m->comp(
            '/Ticket/Elements/PreviewScrips:GetRecipients',
            %$self, ticket_obj => $ticket_obj
        );
    }
    elsif ( $Operation eq 'Create' ) {
        @recipients = $m->comp(
            '/Ticket/Elements/PreviewScrips:GetRecipientsOnCreate',
            %$self,
        );
    }
    else {
        Jifty->log->fatal('Incorrect operation: '. $Operation );
    }

    my %seen;
    @recipients = grep !$seen{ lc $_ }++, @recipients;

    RT::Crypt::GnuPG::use_key_for_encryption(
        map { (/^UseKey-(.*)$/)[0] => $self->{ $_ } }
        grep $self->{ $_ } && /^UseKey-/,
        keys %$self
    );

    my ($status, @issues) = RT::Crypt::GnuPG::check_recipients( @recipients );
    push @{ $self->{'GnuPGRecipientsKeyIssues'} ||= [] }, @issues;
    $checks_failure = 1 unless $status;
}

return $checks_failure ? 0 : 1;
</%INIT>
</%METHOD>
